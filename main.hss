# OHR Community Point & Click Adventure
# main.hss
# file created by kylekrack
# Includes main game loop, keypress handler, and autorun scripts

include, movement.hss
include, ui.hss
include, animate.hss
include, util.hss

# CONFIGURATION SETTINGS
defineConstant, begin
    0,config:heroidle       # whether heroes animate while not moving (1 = true)
    -1,config:herosetidle   # walkabout sprite set to use when hero is still (-1 if using picture from custom)
    -1,config:herosetmoving # walkabout sprite set to use when hero is moving (-1 if using picture from custom)
    2,config:walkSpeed      # ticks between each frame while walking    
    3,config:idleSpeed      # ticks between each frame while idling
    3,config:markerSprite   # walkabout sprite set to use for the destinationMarker
end

globalVariable, begin
    0,lmbDown
    1,rmbDown
    2,lmbUp
    3,rmbUp
    4,escPress
    5,gameIsRunning
    6,paused
    7,hud
    8,interactCol           # Per-map slice collection that has interactive slices like Look and Door
    9,actionState
    10,destinationMarker     # To be replaced with a lookup code
    11,animatingSlices      # A slice handle for a container that tracks animating slice handles
end

defineConstant, begin
    # Action States - player can choose whether their click means a look, walk, etc. 
    0,state:walk
    1,state:look
    2,state:use
    3,state:talk
    # Movement values
    2,move:speed
    0,move:friction
    # Player hitbox
    10,player:width
    10,player:height
end

plotscript, keypressHandler, begin
    # Gets and processes all button presses
    # Mouse Buttons
    if(mouseRelease(leftButton)) then(
        lmbUp := true
    )
    if(mouseRelease(rightButton)) then(
        rmbUp := true
    )
    if(mouseButton(leftButton)) then(
        lmbDown := true
    )
    if(mouseButton(rightButton)) then(
        rmbDown := true
    )
    
    # Keyboard
    if(keypress(key:esc)) then(
        escPress := true
    )
end

plotscript, newGame, begin
    # Run when the game is run, initializes things like the mouse and hud
    # Initializations
    suspendPlayer
    initMouse
    unhideMouseCursor
    gameIsRunning := true
    setSliceSize(getHeroSlice(me), player:width, player:height)
    hud := loadSliceCollection(0)
    animatingSlices := createContainer(0,0) # a list of animating slice handles
    mainGameLoop
end

script, mainGameLoop, begin
    # Runs every in-game tick, calls to functions from outside this file
    variable(x, y, sl, oldPlayerX, oldPlayerY, lastPlayerX, lastPlayerY, playerMoved)
    # Init old position to -1, to indicate no movement has ocurred
    oldPlayerX := -1
    oldPlayerY := -1
   
    while(gameIsRunning) do(
        # Actions within a subscript to allow for mid-game recompiling
        loopContents()
        wait
    )

    subscript, loopContents, begin
        # Mouse
        x := mousePixelX, y := mousePixelY
        mouseHover(x, y)
        
        # Check Paused: everything after this will be skipped if game is paused
        if(paused) then(
            resetInputs()
            wait
            continue
        )
        
        # Pause Skip Line
        # ---------------------------
        
        # Mouse Controls
        if(lmbDown) then(
            # Events when lmb clicked down:
            # * Click to Move
            lmbDownActions(x, y)
        ) elseif(lmbUp) then(
            # Events when lmb lifted:
            # * Look at something
            # * Use item on something
            lmbUpActions(x, y)
        ) elseif(rmbDown) then(
            # Events when rmb clicked down:
            rmbDownActions(x, y)
        ) elseif(rmbUp) then(
            # Events when rmb lifted:
            rmbUpActions(x, y)
        )
        
        # Player Movement
        if(destinationMarker) then(
            # Either player is still moving, is blocked from moving, 
            # or has reached destination
            x := sliceX(getHeroSlice(me)) + (sliceWidth(getHeroSlice(me))/2) # Middle of hero slice
            y := sliceY(getHeroSlice(me)) + sliceHeight(getHeroSlice(me))   # Bottom of hero slice
            checkContinueMove(x, y)
        )

        # Animations
        animatePlayer(playerMoved)
        animateSlices()
        
        # Doors
        # Check if player slice is colliding with a slice on interactCol with 
        # the sli:door lookup code
        sl := findCollidingSlice(interactCol, getHeroSlice(me), 0, true, false)
        if(sl) then(
            if(getSliceLookup(sl) == sli:door) then(
                x := getSliceExtra(sl, extra1) # Store destination x in tiles
                y := getSliceExtra(sl, extra2) # Store destination y in tiles
                useDoor(getSliceExtra(sl, extra0)) # Store door id here
                putHero(me, x*20, y*20)
            )
        )

        # Debug
        if(readPreferenceBit(8) == false) then(
            # Debug keys enabled
            showStringAt(0, 0, 0), showStringAt(1, 0, 10), showStringAt(2, 0, 20), showStringAt(3, 0, 30)
        ) else(
            showNoValue()
        )
        
        resetInputs()
    end
    
    subscript, resetInputs, begin
        lmbDown := false
        rmbDown := false
        lmbUp := false
        rmbUp := false
        escPress := false
        if(readPreferenceBit(8) == false) then(
            clearString(0), clearString(1), clearString(2), clearString(3)
        )
    end
    
    subscript, checkContinueMove, playerX, playerY, begin
        # Checks if player is at the same place as last tick (stuck)
        # or if the player has reached their destination
        # Continue moving if able to and not at destination
        if(oldPlayerX == -1 && oldPlayerY == -1) then(
            # Set player's initial position: when they started moving
            oldPlayerX := playerX
            oldPlayerY := playerY
            # Set most recent player position
            lastPlayerX := playerX
            lastPlayerY := playerY
            # Move towards destination marker
            x := (sliceX(destinationMarker) + (sliceWidth(destinationMarker)/2)) -- playerX # The center of the ellipse -- player x
            y := (sliceY(destinationMarker) + (sliceHeight(destinationMarker)/2)) -- playerY # The center of the ellipse -- player y
            movePlayer(x, y)
            playerMoved := true
        ) elseif((lastPlayerX == playerX && lastPlayerY == playerY) || checkPlayerAtMarker(playerX, playerY)) then(
            # Stuck or reached destination
            oldPlayerX := -1, oldPlayerY := -1 # Reset old values
            # Remove destination marker
            freeSlice(destinationMarker)
            destinationMarker := 0
            playerMoved := false
        ) else(
            # Move towards destination marker
            lastPlayerX := playerX
            lastPlayerY := playerY
            x := (sliceX(destinationMarker) + (sliceWidth(destinationMarker)/2)) -- playerX # The center of the ellipse -- player x
            y := (sliceY(destinationMarker) + (sliceHeight(destinationMarker)/2)) -- playerY # The center of the ellipse -- player y
            movePlayer(x, y)
            playerMoved := true
        )
    end
end

plotscript, mapAutorun, col, begin
    # The default map autorun script. Special autorun scripts should call this one
    # col is the interactive slice collection to load for this map
    if(interactCol) then(freeSlice(interactCol))
    interactCol := loadSliceCollection(col)
end